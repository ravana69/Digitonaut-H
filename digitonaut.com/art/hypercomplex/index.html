
<!DOCTYPE html>
<html lang="en">

<!-- Mirrored from digitonaut.com/art/hypercomplex/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 10:21:58 GMT -->
<!-- Added by HTTrack --><meta http-equiv="content-type" content="text/html;charset=UTF-8" /><!-- /Added by HTTrack -->
<head>
<meta charset='utf-8'\>
<meta name='apple-mobile-web-app-capable' content='yes'><meta name='apple-mobile-web-app-status-bar-style' content='black-translucent'><title>Hypercomplex</title>

  <script src="shader-web-background.min.js"></script>

  <script type="x-shader/x-fragment" id="Image">
    precision highp float;
    uniform vec2  iResolution;
    uniform float iTime;

    // -------------------------------------------------------------------------------------------------- Paste your Shadertoy code here:

// "Hypercomplex" by Alexander Alekseev aka TDM - 2014
// License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.

const int NUM_STEPS = 128;
const int AO_SAMPLES = 3;
const float INV_AO_SAMPLES = 1.0 / float(AO_SAMPLES);
const float EPSILON = 1e-5;
const vec3 RED = vec3(0.6,0.03,0.08);
const vec3 ORANGE = vec3(0.3,0.1,0.1);
const vec3 BG = vec3(0.05,0.05,0.075);

// lighting
float diffuse(vec3 n,vec3 l,float p) { return pow(dot(n,l) * 0.4 + 0.6,p); }
float specular(vec3 n,vec3 l,vec3 e,float s) {    
    float nrm = (s + 8.0) / (3.1415 * 8.0);
    return pow(max(dot(reflect(e,n),l),0.0),s) * nrm;
}
float specular(vec3 n,vec3 e,float s) {    
    float nrm = (s + 8.0) / (3.1415 * 8.0);
    return pow(max(1.0-abs(dot(n,e)),0.0),s) * nrm;
}

// julia based on iq's implementation
float julia(vec3 p,vec4 q) {
    vec4 nz, z = vec4(p,0.0);
    float z2 = dot(p,p), md2 = 1.0;    
    for(int i = 0; i < 11; i++) {
        md2 *= 4.0*z2;
        nz.x = z.x*z.x-dot(z.yzw,z.yzw);
        nz.y = 2.0*(z.x*z.y + z.w*z.z);
        nz.z = 2.0*(z.x*z.z + z.w*z.y);
        nz.w = 2.0*(z.x*z.w - z.y*z.z);
        z = nz + q;
        z2 = dot(z,z);
        if(z2 > 4.0) break;
    }    
	return 0.25*sqrt(z2/md2)*log(z2);    
}

float rsq(float x) {
    x = sin(x);
    return pow(abs(x),3.0) * sign(x);
}

// world
float map(vec3 p) {
    const float M = 0.6;
    float time = iTime + rsq(iTime*0.5) * 2.0;
    return julia(p,vec4( 
        sin(time*0.96456)*0.451*M,
        cos(time*0.59237)*0.435*M,
        sin(time*0.73426)*0.396*M,
        cos(time*0.42379)*0.425*M
    ));
}
    
vec3 getNormal(vec3 p) {
    vec3 n;
    n.x = map(vec3(p.x+EPSILON,p.y,p.z));
    n.y = map(vec3(p.x,p.y+EPSILON,p.z));
    n.z = map(vec3(p.x,p.y,p.z+EPSILON));
    return normalize(n-map(p));
}
float getAO(vec3 p,vec3 n) {    
    const float R = 3.0;
    const float D = 0.8;
    float r = 0.0;
    for(int i = 0; i < AO_SAMPLES; i++) {
        float f = float(i)*INV_AO_SAMPLES;
        float h = 0.1+f*R;
        float d = map(p + n * h);
        r += clamp(h*D-d,0.0,1.0) * (1.0-f);
    }    
    return clamp(1.0-r,0.0,1.0);
}

float spheretracing(vec3 ori, vec3 dir, out vec3 p) {
    float t = 0.0;    
    for(int i = 0; i < NUM_STEPS; i++) {
        p = ori + dir * t;
        float d = map(p);
        if(d <= 0.0 || t > 2.0) break;
        t += max(d*0.3,EPSILON);
    }    
    return step(t,2.0);
}

void mainImage( out vec4 fragColor, in vec2 fragCoord ) {
	vec2 uv = fragCoord.xy / iResolution.xy;
    uv = uv * 2.0 - 1.0;
    uv.x *= iResolution.x / iResolution.y;    
    float time = iTime * 0.1;
    vec2 sc = vec2(sin(time),cos(time));
    
    // tracing of distance map
    vec3 p;
    vec3 ori = vec3(0.0,0.0,1.5);
    vec3 dir = normalize(vec3(uv.xy,-1.0));    
    ori.xz = vec2(ori.x * sc.y - ori.z * sc.x, ori.x * sc.x + ori.z * sc.y);
    dir.xz = vec2(dir.x * sc.y - dir.z * sc.x, dir.x * sc.x + dir.z * sc.y);
    
    float mask = spheretracing(ori,dir,p);
    vec3 n = getNormal(p);
    float ao = pow(getAO(p,n), 2.2);
    ao *= n.y * 0.5 + 0.5;
    
    // bg    
    vec3 bg = mix(
        mix(vec3(0.0), BG,        
        	smoothstep(-1.0,1.0,uv.y)),
        mix(BG*0.5, vec3(0.0),        
        	smoothstep(-1.0,1.0,uv.y)),
        smoothstep(-1.0,1.0,uv.x));      
    bg *= 0.8 + 0.2 * smoothstep(0.1,0.0,sin((uv.x-uv.y)*40.0));
    
    // color
    vec3 l0 = normalize(vec3(-0.0,0.0,-1.0));
    vec3 l1 = normalize(vec3(0.3,0.5,0.5));
    vec3 l2 = normalize(vec3(0.0,1.0,0.0));
    vec3 color = RED * 0.4;
    color += specular(n,l0,dir,1.0) * RED;
    color += specular(n,l1,dir,1.0) * ORANGE * 1.1; 
    color = color*ao*4.0;
    
    color = mix(bg,color,mask);
        
    //color = vec3(ao);
    //color = n * 0.5 + 0.5;
	
	fragColor = vec4(pow(color,vec3(0.4545)),1.0);
}
    // -------------------------------------------------------------------------------------------------- End of Shadertoy code


    void main() {
      mainImage(gl_FragColor, gl_FragCoord.xy);
    }
  </script>
  <script>
    shaderWebBackground.shade({
      shaders: {
        Image: {
          uniforms: {
            iResolution: (gl, loc, ctx) => gl.uniform2f(loc, ctx.width, ctx.height),
            iTime:       (gl, loc) => gl.uniform1f(loc, performance.now() / 1000),
          }
        }
      }
    });
  </script>
</head>
<body>
</body>

<!-- Mirrored from digitonaut.com/art/hypercomplex/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 10:21:59 GMT -->
</html>
